import com.amazonaws.auth.*

buildscript {
    dependencies {
        classpath 'com.amazonaws:aws-java-sdk-core:1.11.261'
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm'  version '1.1.61'
    id 'com.github.johnrengelman.shadow' version '2.0.1'
    id 'io.spring.dependency-management' version '1.0.3.RELEASE'
    id 'org.junit.platform.gradle.plugin' version '1.0.2'
}

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'maven-publish'

mainClassName = 'lt.tlistas.loginn.backend.ApplicationKt'

defaultTasks 'run'

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

def fetchAwsCredentials = {
    try {
        return DefaultAWSCredentialsProviderChain.getInstance().credentials
    } catch (Exception e) {
        println e
        logger.error('Unable to retrieve AWS credentials from profile, publishing to S3 will not be available.', e)
        return null
    }
}
AWSCredentials awsCredentials = fetchAwsCredentials()
repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://repo.spring.io/milestone" }
    repositories {
        maven {
            url 's3://repository.tlist.no/releases'
            credentials(AwsCredentials) {
                accessKey awsCredentials.AWSAccessKeyId
                secretKey awsCredentials.AWSSecretKey
            }
        }
    }
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.boot:spring-boot-dependencies:2.0.0.M7'
    }
}

dependencies {
    compile "lt.tlistas:core:1.1.1"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8"
    compile "org.jetbrains.kotlin:kotlin-reflect"

    compile "org.springframework:spring-webflux"
    compile ("org.springframework:spring-context") {
        exclude group: "spring-aop"
    }
    compile "io.projectreactor.ipc:reactor-netty"

    compile "org.slf4j:slf4j-api"
    compile "ch.qos.logback:logback-classic"

    compile "com.fasterxml.jackson.module:jackson-module-kotlin"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"

    testCompile "io.projectreactor:reactor-test"

    testCompile "org.junit.jupiter:junit-jupiter-api"
    testRuntime "org.junit.jupiter:junit-jupiter-engine"
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'lt.tlistas.loginn'
            artifactId 'backend'
            version '1.1.1-RELEASE'
            from components.java

            repositories {
                maven {
                    url 's3://repository.tlist.no/releases'
                    credentials(AwsCredentials) {
                        accessKey = awsCredentials.AWSAccessKeyId
                        secretKey = awsCredentials.AWSSecretKey
                    }
                }
            }
        }
    }
}
